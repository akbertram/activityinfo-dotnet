plugins {
  id "com.ullink.msbuild" version "2.21"
  id 'com.ullink.nunit' version '1.12'
  id "com.ullink.nuget" version "2.17"
}

def buildNumber = System.getenv('BUILD_NUMBER')
if(buildNumber) {
   project.version = "3.0.${buildNumber}"
} else {
   project.version = "3.dev"
}


// Update the library version in AssemblyInfo.cs
assemblyInfoPatcher {
    if(!System.getenv('BUILD_NUMBER')) {
      enabled = false
    }
    version = project.version

    // defaults to above version, fewer restrictions on the format
    fileVersion = version
}

nuget {
    version = '4.7.1'
}

// Not used..
// See https://github.com/Ullink/gradle-nuget-plugin/issues/66
nugetSources {
    operation = 'add'
    sourceName = 'localNuGetFeed'
    sourceUrl = 'http://foo.com'
    username = 'optional username'
    password = 'optional password'
    configFile = 'nuget.config'
}

msbuild {
    
    dependsOn 'nugetRestore'
    dependsOn 'assemblyInfoPatcher'
    
    solutionFile = "${projectDir}/ActivityInfoDotNet.sln"

    projectName = 'Client'
    
    // targets to execute (/t:Clean;Rebuild, no default)
    targets = ['Clean', 'Rebuild']

    // Below values can override settings from the project file

    // overrides project OutputPath
    destinationDir = 'build/msbuild/bin'

    // overrides project IntermediaryOutputPath
    intermediateDir = 'build/msbuild/obj'

    // Generates XML documentation file (from javadoc through custom DocLet)
    generateDoc = false
}

nunit {
    testAssemblies = [ msbuild.projects['ActivityInfo.CLient'] ]
    reportFolder = project.file("buildDir/unit-reports")
    logFile = 'TestOutput.log'

}

nugetSpec {
    
    nuspec = [
        metadata: [
            id: 'ActivityInfo.Client',
            version: project.version,
            title: 'ActivityInfo.org Client',
            authors: 'BeDataDriven',
            description: 'API Client for ActivityInfo.org',
            projectUrl: 'https://www.activityinfo.org',
            licenseUrl: 'http://www.gnu.org/licenses/gpl-3.0.en.html',
            iconUrl: 'https://www.activityinfo.org/img/logo.png',
            copyright: 'Copyright Â© BeDataDriven 2010-2018'
        ],
        files: [
            { file (src: "${buildDir}/msbuild/bin/ActivityInfo.Client.dll", target: 'lib') }
        ]
    ]
}

nugetPush {
    nupkgFile = "build/distributions/ActivityInfo.Client.${project.version}.nupkg"
    apiKey = System.getenv("NUGET_API_KEY")
    serverUrl = "https://api.nuget.org/v3/index.json"
}

